@TITLE FNaF Events
@ECHO OFF
SET CNT=1
SET CHICA=0
SET BONNIE=0

ECHO.MO: %CHICA% CHICA
ECHO.MO: %BONNIE% BONNIE

BREAK>MOVEMENTS.cmd

SET MO_CHICA=0
SET MO_BONNIE=0

SETLOCAL ENABLEDELAYEDEXPANSION
:TIMER
TITLE FNaF Events - TIME: !CNT!

SET /A S_CALC=CNT %% 5

SET STATES=
SET /P STATES=<office_states

:: Increase MO based on the timer
IF !CNT! EQU 25 SET /A MO_BONNIE+=1 &:: 0.5 minutes in
IF !CNT! EQU 60 SET /A MO_CHICA+=1 &:: 1.0 minutes in
IF !CNT! EQU 150 SET /A MO_BONNIE+=1 &:: 2.5 minutes in
IF !CNT! EQU 300 SET /A MO_CHICA+=2 &:: 5 minutes in
IF !CNT! EQU 420 SET /A MO_CHICA+=1 &:: 7 minutes in
IF !CNT! EQU 450 SET /A MO_BONNIE+=2 &:: 7.5 minutes in

:: Movement Calculations
IF !S_CALC! EQU 0 (
	SET /A "RND_CHICA=%RANDOM% %% 19 + 1"
	IF !RND_CHICA! LEQ !MO_CHICA! (
		IF !CHICA! LEQ 1 (
			SET /A CHICA+=1
		) ELSE IF !CHICA! GEQ 4 (
			SET /A CHICA+=1
		) ELSE (
			SET /A "RND_CHICA=%RANDOM% %% 2 + 1"
			IF !RND_CHICA! EQU 1 (
				SET /A CHICA+=1
			) ELSE (
				SET /A CHICA-=1
			)
		)
		IF !CHICA! GEQ 5 (ECHO.!STATES! | findstr /C:"doorR") >NUL && SET CHICA=1
		ECHO.MO: !CHICA! CHICA
		>refresh SET /P "=" <NUL
	)
	SET /A "RND_BONNIE=%RANDOM% %% 19 + 1"
	IF !RND_BONNIE! LEQ !MO_BONNIE! (
		IF !BONNIE! EQU 0 (
			SET /A BONNIE+=1
		) ELSE IF !BONNIE! EQU 3 (
			SET /A BONNIE=%RANDOM% %% 3 + 2
		) ELSE IF !BONNIE! EQU 4 (
			SET /A BONNIE+=1
		) ELSE IF !BONNIE! EQU 5 (
			SET /A BONNIE+=1
		) ELSE (
			SET /A BONNIE=%RANDOM% %% 2 + 2
		)
		IF !BONNIE! GEQ 5 (ECHO.!STATES! | findstr /C:"doorL") >NUL && SET /A BONNIE=%RANDOM% %% 3 + 1
		ECHO.MO: !BONNIE! BONNIE
		>refresh SET /P "=" <NUL
	)
	(
		ECHO.SET CHICA=!CHICA!
		ECHO.SET BONNIE=!BONNIE!
	) >MOVEMENTS.cmd
)

:FORCE_REFRESH
IF EXIST .\refresh (
	TASKKILL /IM xcopy.exe /F && DEL /Q .\refresh || GOTO :FORCE_REFRESH
)

:FORCE_REFRESH_
IF !CNT! GEQ 530 (
	IF NOT EXIST WIN BREAK>WIN
	TASKKILL /IM xcopy.exe /F || GOTO :FORCE_REFRESH_
	ENDLOCAL
	EXIT 0
)

SET /A CNT+=1
TIMEOUT /T 1 >NUL
GOTO :TIMER
